plugins {
    id 'io.freefair.lombok' version '6.5.0.2' apply false
    id "io.freefair.javadocs" version "6.5.0.2" apply false
    id "io.freefair.aggregate-javadoc" version "6.5.0.2"
    id 'maven-publish'
}

allprojects {
    group = 'com.strategyobject.substrateclient'
    version = '0.2.4.19-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'io.freefair.javadocs'
    apply plugin: 'maven-publish'

    targetCompatibility = '1.8'
    sourceCompatibility = '1.8'

    dependencies {
        implementation 'com.google.guava:guava:31.1-jre'
        implementation 'org.slf4j:slf4j-api:1.7.36'

        testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
        testImplementation 'org.assertj:assertj-core:3.23.1'
        testImplementation 'org.mockito:mockito-core:4.6.1'
        testImplementation 'org.mockito:mockito-inline:4.6.1'
        testImplementation 'org.slf4j:slf4j-simple:1.7.36'
    }

    test {
        useJUnitPlatform()
    }

    java {
        withSourcesJar()
    }

    publishing {
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/LibertyDSNP/substrate-client-java"
                credentials {
                    username = System.getenv("USERNAME")
                    password = System.getenv("TOKEN")
                }
            }
        }
    }
}
